use std::str::FromStr;

use crate::syntax::*;

grammar;

pub Type: GradualType = {
    <dom:TypeAtomic> "->" <cod:Type> => GradualType::fun(dom, cod),
    <TypeAtomic>,
}

TypeAtomic: GradualType = {
    "?" => GradualType::Dyn(),
    "bool" => GradualType::Base(BaseType::Bool),
    "int" => GradualType::Base(BaseType::Int),
    "string" => GradualType::Base(BaseType::String),
    "[" <Type> "]" => GradualType::list(<>),
    "(" <Type> ")",
}

Args: Vec<(Variable, Option<GradualType>)> = {
    <x:Id> <t:TypeAnnot> => vec![(x, Some(t))],
    <Arg+>
}

Arg: (Variable, Option<GradualType>) = {
    <Id> => (<>, None),
    "(" <x:Id> <t:TypeAnnot?> ")" => (x, t),
}

RecDefns: Vec<(Variable, Option<GradualType>, SourceExpr)> = {
    <v:(<RecDefn> "and")*> <last:RecDefn> => {
        let mut v = v;
        v.push(last);
        v
    }
}

RecDefn: (Variable, Option<GradualType>, SourceExpr) = {
    <Id> <TypeAnnot?> "=" <Expr>,
    <f:Id> <args:Args> "=" <e:Expr> => (f, None, GradualExpr::lams(args, e)),
}

TypeAnnot: GradualType = {
    ":" <Type> => <>,
}

pub Expr: SourceExpr = {
    "if" <c:Expr> "then" <t:Expr> "else" <e:Expr> => GradualExpr::if_(c, t, e),
    "\\" <args:Args> "." <e:Expr> => GradualExpr::lams(args, e),
    "assume" <x:Id> <t:TypeAnnot?> "in" <e2:Expr> => GradualExpr::let_(x.clone(), t, GradualExpr::Hole("__".to_string() + &x, None), e2),
    "let" <x:Id> <t:TypeAnnot?> "=" <e1:Expr> "in" <e2:Expr> => GradualExpr::let_(x, t, e1, e2),
    "let" <f:Id> <args:Args> "=" <e1:Expr> "in" <e2:Expr> => GradualExpr::let_(f, None, GradualExpr::lams(args, e1), e2),
    "let" "rec" <defns:RecDefns> "in" <e2:Expr> => GradualExpr::letrec(defns, e2),
    <ExprAnn>,
}

ExprAnn: SourceExpr = {
    <e:ExprOr> ":" <t:Type> => GradualExpr::ann(e, Some(t)),
    <ExprOr>,
}

ExprOr: SourceExpr = {
    <e1:ExprOr> "||" <e2:ExprAnd> => GradualExpr::bop(SourceBOp::Or, e1, e2),
    <ExprAnd>,
}

ExprAnd: SourceExpr = {
    <e1:ExprAnd> "&&" <e2:ExprCompare> => GradualExpr::bop(SourceBOp::And, e1, e2),
    <ExprCompare>,
}

ExprCompare: SourceExpr = {
    <e1:ExprFactor> "==" <e2:ExprFactor> => GradualExpr::bop(SourceBOp::Equal, e1, e2),
    <e1:ExprFactor> "<" <e2:ExprFactor> => GradualExpr::bop(SourceBOp::LessThan, e1, e2),
    <e1:ExprFactor> ">" <e2:ExprFactor> => GradualExpr::bop(SourceBOp::LessThan, e2, e1),
    <e1:ExprFactor> "<=" <e2:ExprFactor> => GradualExpr::bop(SourceBOp::LessThanEqual, e1, e2),
    <e1:ExprFactor> ">=" <e2:ExprFactor> => GradualExpr::bop(SourceBOp::LessThanEqual, e2, e1),
    <ExprFactor>,
}

ExprFactor: SourceExpr = {
   <e1:ExprFactor> "+" <e2:ExprTerm> => GradualExpr::bop(SourceBOp::Plus, e1, e2),
   <e1:ExprFactor> "-" <e2:ExprTerm> => GradualExpr::bop(SourceBOp::Minus, e1, e2),
   <ExprTerm>,
}

ExprTerm: SourceExpr = {
   <e1:ExprTerm> "*" <e2:ExprApp> => GradualExpr::bop(SourceBOp::Times, e1, e2),
   <e1:ExprTerm> "/" <e2:ExprApp> => GradualExpr::bop(SourceBOp::Divide, e1, e2),
   <ExprApp>,
}

ExprApp: SourceExpr = {
    <e1:ExprApp> <e2:ExprAtom> => GradualExpr::app(e1, e2),
    <ExprUnary>,
};

ExprUnary: SourceExpr = {
    <op:UOp> <e:ExprAtom> => GradualExpr::uop(op, e),
    <ExprAtom>,
}

UOp: SourceUOp = {
    "-" => SourceUOp::Negate,
    "!" => SourceUOp::Not,
    "bool?" => SourceUOp::Is(GroundType::Base(BaseType::Bool)),
    "int?" => SourceUOp::Is(GroundType::Base(BaseType::Int)),
    "string?" => SourceUOp::Is(GroundType::Base(BaseType::String)),
    "fun?" => SourceUOp::Is(GroundType::Fun),
}

ExprAtom: SourceExpr = {
    <Const>,
    <Hole>,
    <Id> => GradualExpr::Var(<>),
    "(" <Expr> ")",
}

Const: SourceExpr = {
    Num => GradualExpr::Const(Constant::Int(<>)),
    Bool => GradualExpr::Const(Constant::Bool(<>)),
    StrLit => GradualExpr::Const(Constant::String(<>)),
}

Num: isize = r"-?[0-9]+" => isize::from_str(<>).unwrap();

Bool: bool = {
    "true" => true,
    "false" => false,
};

// TODO custom lexer to support escapes
StrLit: String = <r#""[^"]*""#> => {
    let s = <>;
    s[1..s.len()-1].to_string()
};

Hole: SourceExpr = {
  r"__[[:alnum:]_']*" => GradualExpr::Hole(<>.to_string(), None),
};

Id: Variable = <r"[[:alpha:]_][[:alnum:]_']*"> => <>.to_string();

match {
    "?",
    "->",
    "(",
    ")",
    "[",
    "]",
    "\\",
    ".",
    ":",
    "=",
    "!",
    "~",
    "+",
    "-",
    "*", 
    "/",
    "&&",
    "||",
    "==",
    "<",
    "<=",
    ">",
    ">=",
} else {
    "dyn",
    "bool",
    "int",
    "string",
    "bool?",
    "int?",
    "string?",
    "fun?",
    "if",
    "then",
    "else",
    "assume",
    "let",
    "rec",
    "and",
    "in",
    "true",
    "false",
    r"__[[:alnum:]_']*",
    r#""[^"]*""#,
} else {
    r"-?[0-9]+",
    r"[[:alpha:]_][[:alnum:]_']*",
}